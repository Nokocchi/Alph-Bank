import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id "io.freefair.lombok" version "9.0.0"
    id 'org.springframework.cloud.contract' version '4.3.0'
    id 'org.openapi.generator' version '7.17.0'
}

group = 'com.alphbank'
version = '0.0.1-SNAPSHOT'

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
    set('bankingCoreAPIVersion', '0.0.4')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

configurations {
    yamlApi
}

dependencies {
    // Base
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.projectreactor:reactor-core:3.6.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Database
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.flywaydb:flyway-core:10.10.0'
    implementation 'org.flywaydb:flyway-database-postgresql:10.10.0'
    implementation 'org.postgresql:r2dbc-postgresql:1.0.4.RELEASE'
    runtimeOnly 'org.postgresql:postgresql'

    // RabbitMQ
    implementation 'org.springframework.boot:spring-boot-starter-amqp:3.2.4'

    // Alph Bank starters
    implementation 'com.alphbank:alph-spring-boot-starter-commons:0.0.22-SNAPSHOT'

    implementation 'io.projectreactor.addons:reactor-extra:3.5.1'
    yamlApi "com.alphbank.core:BankingCore:${bankingCoreAPIVersion}:openapi-spec@yaml"

    // Testing
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'
}

tasks.register('copyYaml', Copy) {
    from configurations.yamlApi
    into "$buildDir/openapi"
}


contracts {
    failOnNoContracts = false
}

tasks.register('openApiGenerateClients', GenerateTask) {
    inputSpec = file("$buildDir/openapi/BankingCore-${bankingCoreAPIVersion}-openapi-spec.yaml").toURI().toString()
    outputDir = "$buildDir/generated/banking-core".toString()
    apiPackage = "com.alphbank.core.client"
    modelPackage = "com.alphbank.core.client.model"
    generatorName = "java"
    apiNameSuffix = "Client"
    modelNameSuffix = "DTO"
    typeMappings = [
            "DateTime": "LocalDateTime",
    ]
    importMappings = [
            "LocalDateTime": "java.time.LocalDateTime",
    ]
    configOptions = [
            booleanGetterPrefix   : "is",
            dateLibrary           : "java8",
            reactive              : "true",
            library               : "webclient",
            useSpringBoot3        : "true",
            useJakartaEe          : "true",
            containerDefaultToNull: "true",
            openApiNullable       : "false", // Otherwise it tries to use annotation that is not imported
            generateBuilders      : "true"
    ]
}

tasks.register('openApiGenerateAPI', GenerateTask) {
    inputSpec = file("$projectDir/src/main/resources/psd2-api_v1.3.15-2025-08-21.openapi.yaml").toURI().toString()
    outputDir = "$buildDir/generated/psd2".toString()
    modelPackage = "org.berlingroup.rest.model"
    generatorName = "spring"
    modelNameSuffix = "DTO"
    openapiNormalizer = ["FILTER": "operationId:createSigningBasket|getSigningBasket|deleteSigningBasket|startSigningBasketAuthorisation|getSigningBasketScaStatus|initiatePayment|getPaymentInformation|cancelPayment|getPaymentInitiationStatus",]
    typeMappings = [
            "DateTime": "LocalDateTime",
    ]
    importMappings = [
            "LocalDateTime": "java.time.LocalDateTime",
    ]
    configOptions = [
            booleanGetterPrefix   : "is",
            annotationLibrary     : "swagger2",
            dateLibrary           : "java8",
            useResponseEntity     : "false",
            reactive              : "true",
            library               : "spring-boot",
            useSpringBoot3        : "true",
            interfaceOnly         : "true",
            useTags               : "true",
            useJakartaEe          : "true",
            containerDefaultToNull: "true",
            openApiNullable       : "false", // Otherwise it tries to use annotation that is not imported
            generateBuilders      : "true"
    ]
}

// Add the generated sources to the main compile classpath
sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/banking-core/src/main/java")
            srcDir("$buildDir/generated/psd2/src/main/java")
        }
    }
}

tasks.sourcesJar {
    dependsOn tasks.openApiGenerateClients
    dependsOn tasks.openApiGenerateAPI
}

tasks.openApiGenerateClients {
    dependsOn tasks.copyYaml
}

tasks.compileJava {
    dependsOn tasks.openApiGenerateClients
    dependsOn tasks.openApiGenerateAPI
}

tasks.named('test') {
    useJUnitPlatform()
}
